// Copyright 2018-2019, Wayfair GmbH
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

use crate::ast::*;
use crate::lexer;
use crate::lexer::{Token};
use crate::pos::Location;
use crate::tilde::Extractor;
use simd_json::OwnedValue;

grammar<'input>;

pub Script : Script1 = {
    <exprs:Exprs> "<end-of-stream>"? => Script1{exprs},
}

Exprs: Exprs1 = {
    <expr:Expr> ";" <list:Exprs> => {
       let mut list = list;
        list.insert(0,expr);
        list
    },
    <item:Expr> ";"? => vec![item],
}

Expr: Expr1 = {
    <o:BinOp<BinOr, Expr, AndExpr>> => <>,
    AndExpr => <>,
}

BinOr: BinOpKind = {
    "or" => BinOpKind::Or,
}

BinAnd: BinOpKind = {
    "and" => BinOpKind::And,
}

AndExpr: Expr1 = {
    BinOp<BinAnd, AndExpr, EqExpr> => <>,
    EqExpr => <>,
}

BinEq: BinOpKind = {
    "==" => BinOpKind::Eq,
    "!=" => BinOpKind::NotEq,
}

EqExpr: Expr1 = {
    BinOp<BinEq, EqExpr, CmpExpr> => <>,
    CmpExpr => <>,
}

BinCmp: BinOpKind = {
    ">=" => BinOpKind::Gte,
    ">" => BinOpKind::Gt,
    "<=" => BinOpKind::Lte,
    "<" => BinOpKind::Lt,
}

CmpExpr: Expr1 = {
    BinOp<BinCmp, CmpExpr, AddExpr> => <>,
    AddExpr => <>,
}

BinAdd: BinOpKind = {
    "+" => BinOpKind::Add,
    "-" => BinOpKind::Sub,
}

AddExpr: Expr1 = {
    BinOp<BinAdd, AddExpr, MulExpr> => <>,
    MulExpr => <>,
}

BinMul: BinOpKind = {
    "*" => BinOpKind::Mul,
    "/" => BinOpKind::Div,
    "%" => BinOpKind::Mod,
}

MulExpr: Expr1 = {
    BinOp<BinMul, MulExpr, UnaryExpr> => <>,
    UnaryExpr => <>,
}

UnaryExpr: Expr1 = {
    <start:@L> "+" <expr:UnarySimpleExpr> <end:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Plus, expr: Box::new(expr), start, end }),
    <start:@L> "-" <expr:UnarySimpleExpr> <end:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Minus, expr: Box::new(expr), start, end }),
    UnarySimpleExpr => <>,
}

UnarySimpleExpr: Expr1 = {
    <start:@L> "not" <expr:PresenceSimplExpr> <end:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Not, expr: Box::new(expr), start, end }),
    PresenceSimplExpr => <>,
}

PresenceSimplExpr: Expr1 = {
    <start:@L> "present" <path:Path> <end:@L> => Expr1::Present{path, start, end},
    <start:@L> "absent" <path:Path> <end:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Not, expr: Box::new(Expr1::Present{path, start, end}), start, end }),
    SimpleExpr => <>,
}


SimpleExpr: Expr1 = {
    <literal:Literal> => Expr1::Literal(literal),
    "(" <expr:Expr> ")" => expr,
    <path:Path> => Expr1::Path(path),
    <record:Record> => Expr1::RecordExpr(record),
    <pp:Match> => Expr1::MatchExpr(Box::new(pp)),
    <pp:Patch> => Expr1::PatchExpr(Box::new(pp)),
    <pp:Merge> => Expr1::MergeExpr(Box::new(pp)),
    <call:Invoke> => Expr1::Invoke(call),
    <comprehension:Foreach> => Expr1::Comprehension(comprehension),
    Let => <>,
    Drop => <>,
    Emit => <>,
}

SimpleExprNoSideffect: Expr1 = {
    <literal:Literal> => Expr1::Literal(literal),
    "(" <expr:Expr> ")" => expr,
    <path:Path> => Expr1::Path(path),
    <record:Record> => Expr1::RecordExpr(record),
    <pp:Match> => Expr1::MatchExpr(Box::new(pp)),
    <pp:Patch> => Expr1::PatchExpr(Box::new(pp)),
    <pp:Merge> => Expr1::MergeExpr(Box::new(pp)),
    <call:Invoke> => Expr1::Invoke(call),
    <comprehension:Foreach> => Expr1::Comprehension(comprehension),
}

Literal: Literal1 = {
    <start:@L> <v:Nil> <end:@L> => Literal1{value: v, start, end},
    <start:@L> <v:Bool> <end:@L> => Literal1{value: v, start, end},
    <start:@L> <v:Int> <end:@L> => Literal1{value: v, start, end},
    <start:@L> <v:Float> <end:@L> => Literal1{value: v, start, end},
    <start:@L> <v:String> <end:@L> => Literal1{value: v, start, end},
    <start:@L> <v:List> <end:@L> => Literal1{value: v, start, end},
}

AssignOp: Token<'input> = {
    "=" => Token::Eq,
}

Let: Expr1 = {
// TODO FIXME investigate multi-let after `event` keyword +change
//    <start:@L> "let" <exprs:Assignments> <end:@L> "end" => Expr::Let(Let { exprs, start, end }),
    <start:@L> "let" <expr:Assignment> <end:@L> => expr, // Expr::Let(Let { exprs: vec![expr], start, end }),
}

// Assignments: Exprs = {
//     <list:Assignments> "," <expr:Assignment> => {
//         let mut list = list;
//         list.push(expr);
//         list
//     },
//     Assignment => vec![<>],
// }

Assignment: Expr1 = {
    <start:@L> <path:Path> @L AssignOp @R <expr:SimpleExpr> <end:@L> => Expr1::Assign(Assign1 { path, expr: Box::new(expr), start, end }),
}

Drop: Expr1 = {
    <start:@L> "drop" <end:@L> => Expr1::Drop{start, end } ,
}

Emit: Expr1 = {
    <start:@L> "emit" <expr:SimpleExprNoSideffect> "=>" <port:StringLiteral> <end:@L> => Expr1::Emit(EmitExpr1 { expr: Box::new(expr), start, end, port: Some(port) }),
    <start:@L> "emit" <expr:SimpleExprNoSideffect>  <end:@L> => Expr1::Emit(EmitExpr1 { expr: Box::new(expr), start, end, port: None }),

    <start:@L> "emit" "=>" <port:StringLiteral> <end:@L> => Expr1::Emit(EmitExpr1 { expr: Box::new(Expr1::Path(Path1::Event(EventPath1 { segments: vec![], start, end }))), start, end, port: Some(port) }) ,
    <start:@L> "emit" <end:@L> => Expr1::Emit(EmitExpr1 { expr: Box::new(Expr1::Path(Path1::Event(EventPath1 { segments: vec![], start, end }))), start, end, port: None }) ,
}

Nil: LiteralValue1 = {
    <literal:"nil">  => LiteralValue1::Native(OwnedValue::Null),
}

Bool: LiteralValue1 = {
    <literal:"bool"> => LiteralValue1::Native( OwnedValue::from(literal) ),
}

Int: LiteralValue1 = {
     <literal:"int">  => LiteralValue1::Native( OwnedValue::from(literal as i64) ),
}

Float: LiteralValue1 = {
    <literal:"float"> => LiteralValue1::Native( OwnedValue::from(literal.0) ),
}

String: LiteralValue1 = {
    <literal:StringLiteral>  => LiteralValue1::Native( OwnedValue::from(literal) ),
}

Path: Path1 = {
    <start:@L> "$" <segments:QualifiedSegments> <end:@L> => Path1::Meta(MetadataPath1 { segments, start, end }),
    <start:@L> "event" <segments:LocalSegments> <end:@L> => Path1::Event(EventPath1 { segments, start, end }),
    <start:@L> "event" <end:@L> => Path1::Event(EventPath1 { segments: vec![], start, end }),
    <start:@L> "$" <end:@L> => Path1::Meta(MetadataPath1 { segments: vec![], start, end }),
    <start:@L> <segment:PathSegment> <segments:LocalSegments> <end:@L> => {
        let mut list = segments;
        list.insert(0, segment);
        Path1::Local(LocalPath1 { segments: list, start, end })
    },
    <start:@L> <segment:PathSegment> <end:@L> => {
        Path1::Local(LocalPath1 { segments:  vec![segment], start, end })
    },
}

LocalSegments: Segments1 = {
    "." <segment:PathSegment> <segments:LocalSegments> => {
        let mut segments = segments;
        segments.insert(0, segment);
        segments
    },
    "[" <selector:Selector> "]" <segments:LocalSegments> => {
        let mut segments = segments;
        segments.insert(0, selector);
        segments
    },
    "[" <selector:Selector> "]" => vec![selector],
    "." <segment:PathSegment> => vec![segment],
}

Selector: Segment1 = {
    <start_lower:@L> <range_start:Expr> <end_lower:@L> ":" <start_upper:@L> <range_end:Expr> <end_upper:@L> => Segment1::RangeSelector{range_start, range_end, start_lower, end_lower, start_upper, end_upper},
    <start:@L> <expr:Expr> <end:@L>  => Segment1::ElementSelector{expr, start, end},
}

QualifiedSegments: Segments1 = {
    <segments:QualifiedSegments> "." <segment:PathSegment> => {
        let mut list = segments;
        list.push(segment);
        list
    },
    <segments:QualifiedSegments> "[" <segment:Selector> "]" => {
        let mut list = segments;
        list.push(segment);
        list
    },
    "["<start:@L> <segment:Selector> <end:@L>"]" => vec![segment],
    <segment:PathSegment> => vec![segment],
}

#[inline]
PathSegment: Segment1 = {
    <start:@L> <id:Ident> <end:@L> => Segment1::from_id(id, start, end),
}
// ArraySegment: Exprs = {
//     <expr:Expr> ":" <exprs:ArraySegment> => {
//         let mut exprs = exprs;
//         exprs.insert(0, expr);
//         exprs
//     },
//     <expr:Expr> => vec![expr],
// }

ListElements: Exprs1 = {
    <list:ListElements> "," <item:Expr> => {
        let mut list = list;
        list.push(item);
        list
    },
    Expr => vec![<>],

}

List: LiteralValue1 = {
    "[" <exprs:ListElements> "]" => LiteralValue1::List(exprs),
    "[" "]" => LiteralValue1::List(vec![]),
}

ExprOf: Token<'input> = {
    "of" => Token::Of,
}

Match: Match1 = {
  <start:@L> "match" <target:Expr> ExprOf <patterns:Predicates> "end" <end:@L> => Match1 { target, patterns, start, end },
}

Patch: Patch1 = {
  // NOTE: ASSUMPTION array and literal targets are banned
  <start:@L> "patch" <target:Expr> <end:@L> ExprOf <operations:PatchOperations> "end" => Patch1 { target, operations, start, end }
}

Merge: Merge1 = {
  <start:@L> "merge" <target:Expr> ExprOf <expr:Expr> "end" <end:@L> => Merge1 { target, expr, start, end }
}

Foreach: Comprehension1 = {
    <start:@L> "for" <target:Expr> ExprOf <cases:ForeachCaseClauses> "end" <end:@L> => Comprehension1{target: Box::new(target), cases: cases, start, end},
}

ForeachCaseClauses: ComprehensionCases1 = {
    <e:ForeachCaseClause> => vec![e],
    <v:ForeachCaseClauses> <e:ForeachCaseClause> => {
        let mut v = v;
        v.push(e);
        v
    }
}

ForeachCaseClause: ComprehensionCase1 = {
    "case" <start:@L>  "(" <k:Ident> "," <v:Ident> ")" <end:@L> <guard:WhenClause> "=>" <expr:Expr>  => ComprehensionCase1 { key_name: k.id, value_name: v.id, expr: Box::new(expr), guard, start, end },
    "case" <start:@L> "(" <k:Ident> "," <v:Ident> ")" <end:@L> "=>" <expr:Expr> => ComprehensionCase1 { key_name: k.id, value_name: v.id, expr: Box::new(expr), guard: None, start, end },
}

PatchOperations: PatchOperations1 = {
    <e:PatchOperationClause> => vec![e],
    <v:PatchOperations> "," <e:PatchOperationClause> => {
    let mut v = v;
    v.push(e);
    v
  }
}

PatchOperationClause: PatchOperation1 = {
    "insert" <ident:Ident> "=>" <expr:Expr> => PatchOperation1::Insert{ ident: ident.id, expr: expr },
    "upsert" <ident:Ident> "=>" <expr:Expr> => PatchOperation1::Upsert{ ident: ident.id, expr: expr },
    "update" <ident:Ident> "=>" <expr:Expr> => PatchOperation1::Update{ ident: ident.id, expr: expr },
    "erase" <ident:Ident> => PatchOperation1::Erase{ ident: ident.id },
    // "test" <ident:Ident> "=>" <expr:Expr> => PatchOperationClause { kind: PatchOperationKind::Add, ident, Some(expr) },
    // "move" <ident:Ident> <to:RelativePath> => PatchOperationClause { kind: PatchOperationKind::Add, ident, expr: None },
    // "copy" <ident:Ident> <to:RelativePath> => PatchOperationClause { kind: PatchOperationKind::Add, ident, expr: None },
    "merge" <ident:Ident> "=>" <record_expr:Expr> => PatchOperation1::Merge { ident: ident.id, expr: record_expr },
    "merge" "=>" <record_expr:Expr> => PatchOperation1::TupleMerge { expr: record_expr },
}

MatchTarget: Expr1 = {
    <start:@L> <path:Path> AssignOp <expr:Expr> <end:@L> => Expr1::Assign(Assign1 { path, expr: Box::new(expr), start, end }),
    <expr:Expr> => <>,
}

Predicates: Predicates1 = {
    <e:PredicateClause> => vec![e],
    <v:Predicates> <e:PredicateClause> => {
        let mut v = v;
        v.push(e);
        v
    }
}

PredicateClause: PredicateClause1 = {
    <start:@L> "case" <rp:RecordCaseClause> <exprs:Effectors> <end:@L> => PredicateClause1 { pattern: rp, exprs, guard: None, start, end },
    <start:@L> "case" <rp:RecordCaseClause> <guard:WhenClause> <exprs:Effectors> <end:@L> => PredicateClause1 { pattern: rp, exprs, guard, start, end },
    <start:@L> "case" <ap:ArrayCaseClause> <exprs:Effectors> <end:@L> => PredicateClause1 { pattern: ap, exprs, guard: None, start, end },
    <start:@L> "case" <ap:ArrayCaseClause> <guard:WhenClause> <exprs:Effectors> <end:@L> => PredicateClause1 { pattern: ap, exprs, guard, start, end },
    <start:@L> "case" <lhs:ExprCaseClause> <rhs:Effectors> <end:@L> => PredicateClause1 { pattern: lhs, exprs: rhs, guard: None, start, end },
    <start:@L> "case" <lhs:ExprCaseClause> <guard:WhenClause> <rhs:Effectors> <end:@L> => PredicateClause1 { pattern: lhs, exprs: rhs, guard, start, end},
    <start:@L> "default" <exprs:Effectors> <end:@L> => PredicateClause1 { pattern: Pattern1::Default, exprs, guard: None, start, end },
}

RecordCaseClause: Pattern1 = {
    <start:@L> <segment:PathSegment> AssignOp <rp:RecordPattern> <end:@L> => Pattern1::Assign(AssignPattern1 { id: Path1::Local( LocalPath1 { segments: vec![segment], start, end}), pattern: Box::new(Pattern1::Record(rp)) }),
    <rp:RecordPattern> => Pattern1::Record(rp),
}

ArrayCaseClause: Pattern1 = {
    <start:@L> <segment:PathSegment> AssignOp <ap:ArrayPattern> <end:@L> => Pattern1::Assign(AssignPattern1 { id: Path1::Local( LocalPath1 { segments: vec![segment], start, end}), pattern: Box::new(Pattern1::Array(ap)) }),
    <ap:ArrayPattern> => Pattern1::Array(ap),
}

ExprCaseClause: Pattern1 = {
    <start:@L> <segment:PathSegment> AssignOp <expr:SimpleExprNoSideffect> <end:@L> => Pattern1::Assign(AssignPattern1 { id:  Path1::Local( LocalPath1 { segments: vec![segment], start, end}), pattern: Box::new(Pattern1::Expr(expr)) }),
    <expr:SimpleExprNoSideffect> => Pattern1::Expr(expr),
}

WhenClause: Option<Expr1> = {
    "when" <expr:SimpleExprNoSideffect> => Some(expr),
}

Effectors: Exprs1 = {
    "=>" <exprs:Block> => exprs
}

Block: Exprs1 = {
    <e:Expr> => vec![e],
    <v:Block> "," <e:Expr> => {
        let mut v = v;
        v.push(e);
        v
    }
}

PredicateFieldPattern: PredicatePattern1 = {
    <start:@L> <lhs:Ident> <op:"~="> <expr:TestExpr> <end:@L> => PredicatePattern1::TildeEq { assign: lhs.id.clone(), lhs: lhs.id, test: expr},
    <start:@L> <assign:Ident> AssignOp <lhs:Ident> <op:"~="> <expr:TestExpr> <end:@L> => PredicatePattern1::TildeEq { assign: assign.id, lhs: lhs.id, test: expr} ,

    <start:@L> <lhs:Ident> <op:"~="> <rp:RecordPattern> <end:@L> => PredicatePattern1::RecordPatternEq { lhs: lhs.id, pattern: rp },
    <start:@L> <lhs:Ident> <op:"~="> <ap:ArrayPattern> <end:@L> => PredicatePattern1::ArrayPatternEq { lhs: lhs.id, pattern: ap },
    // FIXME: should this be op ~ since the assign is seperate?
    <start:@L> "present" <lhs:Ident> <end:@L> => PredicatePattern1::FieldPresent { lhs: lhs.id },
    <start:@L> "absent" <lhs:Ident> <end:@L> => PredicatePattern1::FieldAbsent { lhs: lhs.id },
    <lhs:Ident> <op:"=="> <expr:Expr> => PredicatePattern1::Eq { lhs: lhs.id, rhs: expr, not: false },
    <lhs:Ident> <op:"!="> <expr:Expr> => PredicatePattern1::Eq { lhs: lhs.id, rhs: expr, not: true },
}

TestExpr: TestExpr1 = {
    <start:@L> <id:Ident> <tl: TestLiteral> <end:@L> => {
        TestExpr1 { id: id.id, test: tl, start, end}
    },
}

RecordPattern: RecordPattern1 = {
    <start:@L> "%{" <fields:PatternFields> "}" <end:@L> => RecordPattern1 { fields: fields, start, end },
    <start:@L> "%{" "}" <end:@L> => RecordPattern1 { fields: vec![], start, end },
}

ArrayPattern: ArrayPattern1 = {
    <start:@L> "%[" <exprs:ArrayPredicatePatterns> "]" <end:@L> => ArrayPattern1 { exprs, start, end },
}

ArrayPredicatePattern: ArrayPredicatePattern1 = {
    "~" <test:TestExpr> => ArrayPredicatePattern1::Tilde(test),
    Expr => ArrayPredicatePattern1::Expr(<>),
    RecordPattern => ArrayPredicatePattern1::Record(<>),
}

ArrayPredicatePatterns: ArrayPredicatePatterns1 = {
    <list:ArrayPredicatePatterns> "," <item:ArrayPredicatePattern> => {
        let mut list = list;
        list.push(item);
        list
    },
    ArrayPredicatePattern => vec![<>],
}


PatternFields: PatternFields1 = {
    <fields:Sep<PatternFields, PredicateFieldPattern, ",">> => fields,
}

Record: Record1 = {
    <start:@L> "{" <fields:Fields> "}" <end:@L> => Record1 { fields: fields, start, end },
    <start:@L> "{" "}" <end:@L> => Record1 { fields: vec![], start, end }
}

Field: Box<Field1> = {
    <start:@L> <name:StringLiteral> ":" <value:Expr> <end:@L> => {
        Box::new(Field1 { name, value: Box::new(value), start, end })
    }
}

Invoke: Invoke1 = {
    <start:@L> <module:Ident> "::" <fun:Ident> "(" <args:InvokeArgs> ")" <end:@L> => Invoke1 { module: module.id, fun: fun.id, args, start, end },
    <start:@L> <module:Ident> "::" <fun:Ident> "(" ")" <end:@L> => Invoke1 { module: module.id, fun: fun.id, args: vec![], start, end },
}

InvokeArgs: Exprs1 = {
    <list:InvokeArgs> "," <item:Expr> => {
        let mut list = list;
        list.push(item);
        list
    },
    Expr => vec![<>],
}

#[inline]
Fields: Fields1 = {
    <fields:Sep<Fields, Field, ",">> => fields,
}

#[inline]
Ident: Ident = {
    "<ident>" => Ident { id: <>.0 },
}

#[inline]
StringLiteral: String = {
    "string" => <>.to_string(),
}

#[inline]
TestLiteral: String = {
    "test" => <>.to_string(),
}

Sep<L, T, D>: L = {
    <e:T> => vec![e],
    <v:L> D <e:T> => {
        let mut v = v;
        v.push(e);
        v
    },
    <v:L> D  => v,
}

#[inline]
BinOp<Op, Current, Next>: Expr1 = {
    <start:@L> <lhs:(<Current>)> <op:(<Op>)> @L <rhs:Next> <end:@L> => Expr1::Binary(BinExpr1 {
        kind: op,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        start,
        end,
    }),
}

extern {
    type Location = Location;
    type Error = crate::errors::Error;
    enum Token<'input> {
        "<doc-comment>" => lexer::Token::DocComment(<&'input str>),
        "<single-line-comment>" => lexer::Token::SingleLineComment(<&'input str>),
        "let" => lexer::Token::Let,
        "match" => lexer::Token::Match,
        "of" => lexer::Token::Of,
        "end" => lexer::Token::End,
        "case" => lexer::Token::Case,
        "when" => lexer::Token::When,
        "for" => lexer::Token::For,
        "nil" => lexer::Token::Nil,
        "and" => lexer::Token::And,
        "or" => lexer::Token::Or,
        "not" => lexer::Token::Not,
        "drop" => lexer::Token::Drop,
        "emit" => lexer::Token::Emit,
        "default" => lexer::Token::Default,
        "patch" => lexer::Token::Patch,
        "insert" => lexer::Token::Insert,
        "upsert" => lexer::Token::Upsert,
        "update" => lexer::Token::Update,
        "erase" => lexer::Token::Erase,
        "merge" => lexer::Token::Merge,
        "event" => lexer::Token::Event,
        "present" => lexer::Token::Present,
        "absent" => lexer::Token::Absent,
        "." => lexer::Token::Dot,
        ";" => lexer::Token::Semi,
        "::" => lexer::Token::ColonColon,
        ":" => lexer::Token::Colon,
        "," => lexer::Token::Comma,
        "=" => lexer::Token::Eq,
//        "_" => lexer::Token::DontCare,
        "==" => lexer::Token::EqEq,
        "!=" => lexer::Token::NotEq,
        "{" => lexer::Token::LBrace,
        "}" => lexer::Token::RBrace,
        "(" => lexer::Token::LParen,
        ")" => lexer::Token::RParen,
        "." => lexer::Token::Dot,
        "$" => lexer::Token::Dollar,
        "=>" => lexer::Token::EqArrow,
        "[" => lexer::Token::LBracket,
        "]" => lexer::Token::RBracket,
        ">=" => lexer::Token::Gte,
        ">" => lexer::Token::Gt,
        "<=" => lexer::Token::Lte,
        "<" => lexer::Token::Lt,
        "+" => lexer::Token::Add,
        "-" => lexer::Token::Sub,
        "*" => lexer::Token::Mul,
        "/" => lexer::Token::Div,
        "%[" => lexer::Token::LPatBracket,
        "%{" => lexer::Token::LPatBrace,
        "%" => lexer::Token::Mod,
        "~=" => lexer::Token::TildeEq,
        "~" => lexer::Token::Tilde,
        "bool" => lexer::Token::BoolLiteral(<bool>),
        "int" => lexer::Token::IntLiteral(<i64>),
        "float" => lexer::Token::FloatLiteral(<f64>, <String>),
        "string" => lexer::Token::StringLiteral(<String>),
        "test" => lexer::Token::TestLiteral(<String>),
        "<ident>" => lexer::Token::Ident(<String>, <bool>),
        "<error>" => lexer::Token::BadToken(<String>),
        "<end-of-stream>" => lexer::Token::EndOfStream,
    }
}

