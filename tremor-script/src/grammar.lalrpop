// Copyright 2018-2019, Wayfair GmbH
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

use crate::ast::*;
use crate::lexer;
use crate::lexer::{Token, LexerError};
use crate::pos::Location;
use crate::tilde::Extractor;
use simd_json::OwnedValue;

grammar<'input>;

pub Script : Script1 = {
    <exprs:Exprs> "<end-of-stream>"? => Script1{exprs},
    <expr:Expr> "<end-of-stream>" => Script1{exprs: vec![expr]},
}

Exprs: Exprs1 = {
    <expr:Expr> ";" <list:Exprs> => {
       let mut list = list;
        list.insert(0,expr);
        list
    },
    <item:Expr> ";" => vec![item],
}

Expr: Expr1 = {
    <o:BinOp<BinOr, Expr, AndExpr>> => <>,
    AndExpr => <>,
}

BinOr: BinOpKind = {
    "or" => BinOpKind::Or,
}

BinAnd: BinOpKind = {
    "and" => BinOpKind::And,
}

AndExpr: Expr1 = {
    BinOp<BinAnd, AndExpr, EqExpr> => <>,
    EqExpr => <>,
}

BinEq: BinOpKind = {
    "==" => BinOpKind::Eq,
    "!=" => BinOpKind::NotEq,
}

EqExpr: Expr1 = {
    BinOp<BinEq, EqExpr, CmpExpr> => <>,
    CmpExpr => <>,
}

BinCmp: BinOpKind = {
    ">=" => BinOpKind::Gte,
    ">" => BinOpKind::Gt,
    "<=" => BinOpKind::Lte,
    "<" => BinOpKind::Lt,
}

CmpExpr: Expr1 = {
    BinOp<BinCmp, CmpExpr, AddExpr> => <>,
    AddExpr => <>,
}

BinAdd: BinOpKind = {
    "+" => BinOpKind::Add,
    "-" => BinOpKind::Sub,
}

AddExpr: Expr1 = {
    BinOp<BinAdd, AddExpr, MulExpr> => <>,
    MulExpr => <>,
}

BinMul: BinOpKind = {
    "*" => BinOpKind::Mul,
    "/" => BinOpKind::Div,
    "%" => BinOpKind::Mod,
}

MulExpr: Expr1 = {
    BinOp<BinMul, MulExpr, UnaryExpr> => <>,
    UnaryExpr => <>,
}

UnaryExpr: Expr1 = {
    <s:@L> "+" <expr:UnarySimpleExpr> <e:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Plus, expr: Box::new(expr), start: s, end: e }),
    <s:@L> "-" <expr:UnarySimpleExpr> <e:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Minus, expr: Box::new(expr), start: s, end: e }),
    UnarySimpleExpr => <>,
}

UnarySimpleExpr: Expr1 = {
    <s:@L> "not" <expr:SimpleExpr> <e:@L> => Expr1::Unary(UnaryExpr1 { kind: UnaryOpKind::Not, expr: Box::new(expr), start: s, end: e }),
    SimpleExpr => <>,
}

SimpleExpr: Expr1 = {
    <literal:Literal> => Expr1::Literal(literal),
    "(" <expr:Expr> ")" => expr,
    <path:Path> => Expr1::Path(path),
    <record:Record> => Expr1::RecordExpr(record),
    <pp:Match> => Expr1::MatchExpr(Box::new(pp)),
    <pp:Patch> => Expr1::PatchExpr(Box::new(pp)),
    <pp:Merge> => Expr1::MergeExpr(Box::new(pp)),
    <call:Invoke> => Expr1::Invoke(call),
    <comprehension:Foreach> => Expr1::Comprehension(comprehension),
    Let => <>,
    Drop => <>,
    Emit => <>,
}

Literal: Literal1 = {
    <s:@L> <v:Nil> <e:@L> => Literal1{value: v, start: s, end: e},
    <s:@L> <v:Bool> <e:@L> => Literal1{value: v, start: s, end: e},
    <s:@L> <v:Int> <e:@L> => Literal1{value: v, start: s, end: e},
    <s:@L> <v:Float> <e:@L> => Literal1{value: v, start: s, end: e},
    <s:@L> <v:String> <e:@L> => Literal1{value: v, start: s, end: e},
    <s:@L> <v:List> <e:@L> => Literal1{value: v, start: s, end: e},
}

AssignOp: Token<'input> = {
    "=" => Token::Eq,
}

Let: Expr1 = {
// TODO FIXME investigate multi-let after `event` keyword +change
//    <s:@L> "let" <exprs:Assignments> <e:@L> "end" => Expr::Let(Let { exprs, start: s, end: e }),
    <s:@L> "let" <expr:Assignment> <e:@L> => expr, // Expr::Let(Let { exprs: vec![expr], start: s, end: e }),
}

// Assignments: Exprs = {
//     <list:Assignments> "," <expr:Assignment> => {
//         let mut list = list;
//         list.push(expr);
//         list
//     },
//     Assignment => vec![<>],
// }

Assignment: Expr1 = {
    <s:@L> <path:Path> @L AssignOp @R <expr:SimpleExpr> <e:@L> => Expr1::Assign(Assign1 { path, expr: Box::new(expr), start: s, end: e }),
}

Drop: Expr1 = {
    <s:@L> "drop" <expr:SimpleExpr> <e:@L> => Expr1::Drop(DropExpr1{ expr: Box::new(expr), start: s, end: e }),
    <s:@L> "drop" <e:@L> => Expr1::Drop(DropExpr1 { expr: Box::new(Expr1::Path(Path1::Event(EventPath1 { segments: vec![], start: s, end: e }))), start: s, end: e }) ,
}

Emit: Expr1 = {
    <s:@L> "emit" <expr:SimpleExpr> <e:@L> => Expr1::Emit(EmitExpr1 { expr: Box::new(expr), start: s, end: e }),
    <s:@L> "emit" <e:@L> => Expr1::Emit(EmitExpr1 { expr: Box::new(Expr1::Path(Path1::Event(EventPath1 { segments: vec![], start: s, end: e }))), start: s, end: e }) ,
}

Nil: LiteralValue1 = {
    <literal:"nil">  => LiteralValue1::Native(OwnedValue::Null),
}

Bool: LiteralValue1 = {
    <literal:"bool"> => LiteralValue1::Native( OwnedValue::from(literal) ),
}

Int: LiteralValue1 = {
     <literal:"int">  => LiteralValue1::Native( OwnedValue::from(literal as i64) ),
}

Float: LiteralValue1 = {
    <literal:"float"> => LiteralValue1::Native( OwnedValue::from(literal) ),
}

String: LiteralValue1 = {
    <literal:StringLiteral>  => LiteralValue1::Native( OwnedValue::from(literal) ),
}

Path: Path1 = {
    <s:@L> "$" <segments:QualifiedSegments> <e:@L> => Path1::Meta(MetadataPath1 { segments, start: s, end: e }),
    <s:@L> "event" "." <segments:QualifiedSegments> <e:@L> => Path1::Event(EventPath1 { segments, start: s, end: e }),
    <s:@L> "event" <e:@R> => Path1::Event(EventPath1 { segments: vec![], start: s, end: e }),
    <s:@L> "$" <e:@L> => Path1::Meta(MetadataPath1 { segments: vec![], start: s, end: e }),
    <s:@L> <segment:PathSegment> <segments:LocalSegments> <e:@L> => {
        let mut list = segments;
        list.insert(0, segment);
        Path1::Local(LocalPath1 { segments: list, start: s, end: e })
    },
    <s:@L> <segment:PathSegment> <e:@L> => {
        Path1::Local(LocalPath1 { segments:  vec![segment], start: s, end: e })
    },
}

LocalSegments: Segments1 = {
    "." <segment:PathSegment> <segments:LocalSegments> => {
        let mut segments = segments;
        segments.insert(0, segment);
        segments
    },
    "[" <selector:Selector> "]" <segments:LocalSegments> => {
        let mut segments = segments;
        segments.insert(0, selector);
        segments
    },
    "[" <selector:Selector> "]" => vec![selector],
    "." <segment:PathSegment> => vec![segment],
}

Selector: Segment1 = {
    <start_lower:@L> <range_start:Expr> <end_lower:@L> ":" <start_upper:@L> <range_end:Expr> <end_upper:@L> => Segment1::RangeSelector{range_start, range_end, start_lower, end_lower, start_upper, end_upper},
    <start:@L> <expr:Expr> <end:@L>  => Segment1::ElementSelector{expr, start, end},
}

QualifiedSegments: Segments1 = {
    <segments:QualifiedSegments> "." <segment:PathSegment> => {
        let mut list = segments;
        list.push(segment);
        list
    },
    <segments:QualifiedSegments> "[" <segment:Selector> "]" => {
        let mut list = segments;
        list.push(segment);
        list
    },
    "["<s:@L> <segment:Selector> <e:@L>"]" => vec![segment],
    <segment:PathSegment> => vec![segment],
}

#[inline]
PathSegment: Segment1 = {
    <s:@L> <id:Ident> <e:@L> => Segment1::from_id(id, s, e),
}
// ArraySegment: Exprs = {
//     <expr:Expr> ":" <exprs:ArraySegment> => {
//         let mut exprs = exprs;
//         exprs.insert(0, expr);
//         exprs
//     },
//     <expr:Expr> => vec![expr],
// }

ListElements: Exprs1 = {
    <list:ListElements> "," <item:Expr> => {
        let mut list = list;
        list.push(item);
        list
    },
    Expr => vec![<>],

}

List: LiteralValue1 = {
    "[" <exprs:ListElements> "]" => LiteralValue1::List(exprs),
    "[" "]" => LiteralValue1::List(vec![]),
}

ExprOf: Token<'input> = {
    "of" => Token::Of,
}

Match: Match1 = {
  <s:@L> "match" <target:Expr> ExprOf <patterns:Predicates> "end" <e:@L> => Match1 { target, patterns, start: s, end: e },
}

Patch: Patch1 = {
  // NOTE: ASSUMPTION array and literal targets are banned
  <s:@L> "patch" <target:Expr> <e:@L> ExprOf <operations:PatchOperations> "end" => Patch1 { target, operations, start: s, end: e }
}

Merge: Merge1 = {
  <s:@L> "merge" <target:Expr> ExprOf <expr:Expr> "end" <e:@L> => Merge1 { target, expr, start: s, end: e }
}

Foreach: Comprehension1 = {
    <s:@L> "for" <target:Expr> ExprOf <cases:ForeachCaseClauses> "end" <e:@L> => Comprehension1{target: Box::new(target), cases: cases, start: s, end: e},
}

ForeachCaseClauses: ComprehensionCases1 = {
    <e:ForeachCaseClause> => vec![e],
    <v:ForeachCaseClauses> <e:ForeachCaseClause> => {
        let mut v = v;
        v.push(e);
        v
    }
}

ForeachCaseClause: ComprehensionCase1 = {
    "case" <start:@L>  "(" <k:Ident> "," <v:Ident> ")" <end:@L> <guard:WhenClause> "=>" <expr:Expr>  => ComprehensionCase1 { key_name: k.id, value_name: v.id, expr: Box::new(expr), guard, start, end },
    "case" <start:@L> "(" <k:Ident> "," <v:Ident> ")" <end:@L> "=>" <expr:Expr> => ComprehensionCase1 { key_name: k.id, value_name: v.id, expr: Box::new(expr), guard: None, start, end },
}

PatchOperations: PatchOperations1 = {
    <e:PatchOperationClause> => vec![e],
    <v:PatchOperations> "," <e:PatchOperationClause> => {
    let mut v = v;
    v.push(e);
    v
  }
}

PatchOperationClause: PatchOperation1 = {
    "insert" <ident:Ident> "=>" <expr:Expr> => PatchOperation1::Insert{ ident: ident.id, expr: expr },
    "upsert" <ident:Ident> "=>" <expr:Expr> => PatchOperation1::Upsert{ ident: ident.id, expr: expr },
    "update" <ident:Ident> "=>" <expr:Expr> => PatchOperation1::Update{ ident: ident.id, expr: expr },
    "erase" <ident:Ident> => PatchOperation1::Erase{ ident: ident.id },
    // "test" <ident:Ident> "=>" <expr:Expr> => PatchOperationClause { kind: PatchOperationKind::Add, ident, Some(expr) },
    // "move" <ident:Ident> <to:RelativePath> => PatchOperationClause { kind: PatchOperationKind::Add, ident, expr: None },
    // "copy" <ident:Ident> <to:RelativePath> => PatchOperationClause { kind: PatchOperationKind::Add, ident, expr: None },
    "merge" <ident:Ident> "=>" <record_expr:Expr> => PatchOperation1::Merge { ident: ident.id, expr: record_expr },
    "merge" "=>" <record_expr:Expr> => PatchOperation1::TupleMerge { expr: record_expr },
}

MatchTarget: Expr1 = {
    <s:@L> <path:Path> AssignOp <expr:Expr> <e:@L> => Expr1::Assign(Assign1 { path, expr: Box::new(expr), start: s, end: e }),
    <expr:Expr> => <>,
}

Predicates: Predicates1 = {
    <e:PredicateClause> => vec![e],
    <v:Predicates> <e:PredicateClause> => {
        let mut v = v;
        v.push(e);
        v
    }
}

PredicateClause: PredicateClause1 = {
    "case" <rp:RecordCaseClause> <exprs:Effectors> => PredicateClause1 { pattern: rp, exprs, guard: None },
    "case" <rp:RecordCaseClause> <guard:WhenClause> <exprs:Effectors> => PredicateClause1 { pattern: rp, exprs, guard },
    "case" <ap:ArrayCaseClause> <exprs:Effectors> => PredicateClause1 { pattern: ap, exprs, guard: None },
    "case" <ap:ArrayCaseClause> <guard:WhenClause> <exprs:Effectors> => PredicateClause1 { pattern: ap, exprs, guard },
    "case" <lhs:ExprCaseClause> <rhs:Effectors> => PredicateClause1 { pattern: lhs, exprs: rhs, guard: None },
    "case" <lhs:ExprCaseClause> <guard:WhenClause> <rhs:Effectors> => PredicateClause1 { pattern: lhs, exprs: rhs, guard},
    "default" <exprs:Effectors> => PredicateClause1 { pattern: Pattern1::Default, exprs, guard: None },
}

RecordCaseClause: Pattern1 = {
    <s:@L> <segment:PathSegment> AssignOp <rp:RecordPattern> <e:@L> => Pattern1::Assign(AssignPattern1 { id: Path1::Local( LocalPath1 { segments: vec![segment], start: s, end: e}), pattern: Box::new(Pattern1::Record(rp)) }),
    <rp:RecordPattern> => Pattern1::Record(rp),
}

ArrayCaseClause: Pattern1 = {
    <s:@L> <segment:PathSegment> AssignOp <ap:ArrayPattern> <e:@L> => Pattern1::Assign(AssignPattern1 { id: Path1::Local( LocalPath1 { segments: vec![segment], start: s, end: e}), pattern: Box::new(Pattern1::Array(ap)) }),
    <ap:ArrayPattern> => Pattern1::Array(ap),
}

ExprCaseClause: Pattern1 = {
    <s:@L> <segment:PathSegment> AssignOp <expr:Expr> <e:@L> => Pattern1::Assign(AssignPattern1 { id:  Path1::Local( LocalPath1 { segments: vec![segment], start: s, end: e}), pattern: Box::new(Pattern1::Expr(expr)) }),
    <expr:Expr> => Pattern1::Expr(expr),
}

WhenClause: Option<Box<Expr1>> = {
    "when" <expr:Expr> => Some(Box::new(expr)),
}

Effectors: Exprs1 = {
    "=>" <exprs:Block> => exprs
}

Block: Exprs1 = {
    <e:Expr> => vec![e],
    <v:Block> "," <e:Expr> => {
        let mut v = v;
        v.push(e);
        v
    }
}

PredicateFieldPattern: PredicatePattern1 = {
    <s:@L> <lhs:Ident> <op:"~="> <expr:TestExpr> <e:@L> => PredicatePattern1::TildeEq { assign: lhs.id.clone(), lhs: lhs.id, test: expr},
    <s:@L> <assign:Ident> AssignOp <lhs:Ident> <op:"~="> <expr:TestExpr> <e:@L> => PredicatePattern1::TildeEq { assign: assign.id, lhs: lhs.id, test: expr} ,

    <s:@L> <lhs:Ident> <op:"~="> <rp:RecordPattern> <e:@L> => PredicatePattern1::RecordPatternEq { lhs: lhs.id, pattern: rp },
    <s:@L> <lhs:Ident> <op:"~="> <ap:ArrayPattern> <e:@L> => PredicatePattern1::ArrayPatternEq { lhs: lhs.id, pattern: ap },
    // FIXME: should this be op ~ since the assign is seperate?
    <s:@L> "present" <lhs:Ident> <e:@L> => PredicatePattern1::FieldPresent { lhs: lhs.id },
    <s:@L> "absent" <lhs:Ident> <e:@L> => PredicatePattern1::FieldAbsent { lhs: lhs.id },
    <lhs:Ident> <op:"=="> <expr:SimpleExpr> => PredicatePattern1::Eq { lhs: lhs.id, rhs: expr, not: false },
    <lhs:Ident> <op:"!="> <expr:SimpleExpr> => PredicatePattern1::Eq { lhs: lhs.id, rhs: expr, not: true },
}

TestExpr: TestExpr1 = {
    <s:@L> <id:Ident> <tl: TestLiteral> <e:@L> => {
        TestExpr1 { id: id.id, test: tl, start: s, end: e}
    },
}

RecordPattern: RecordPattern1 = {
    <s:@L> "%{" <fields:PatternFields> "}" <e:@L> => RecordPattern1 { fields: fields, start: s, end: e },
    <s:@L> "%{" "}" <e:@L> => RecordPattern1 { fields: vec![], start: s, end: e },
}

ArrayPattern: ArrayPattern1 = {
    <s:@L> "%[" <exprs:ArrayPredicatePatterns> "]" <e:@L> => ArrayPattern1 { exprs, start:s, end: e },
}

ArrayPredicatePattern: ArrayPredicatePattern1 = {
    "~" <test:TestExpr> => ArrayPredicatePattern1::Tilde(test),
    Expr => ArrayPredicatePattern1::Expr(<>),
    RecordPattern => ArrayPredicatePattern1::Record(<>),
}

ArrayPredicatePatterns: ArrayPredicatePatterns1 = {
    <list:ArrayPredicatePatterns> "," <item:ArrayPredicatePattern> => {
        let mut list = list;
        list.push(item);
        list
    },
    ArrayPredicatePattern => vec![<>],
}


PatternFields: PatternFields1 = {
    <fields:Sep<PatternFields, PredicateFieldPattern, ",">> => fields,
}

Record: Record1 = {
    <s:@L> "{" <fields:Fields> "}" <e:@L> => Record1 { fields: fields, start: s, end: e },
    <s:@L> "{" "}" <e:@L> => Record1 { fields: vec![], start: s, end: e }
}

Field: Box<Field1> = {
    <s:@L> <name:StringLiteral> ":" <value:Expr> <e:@L> => {
        Box::new(Field1 { name, value: Box::new(value), start: s, end: e })
    }
}

Invoke: Invoke1 = {
    <s:@L> <module:Ident> "::" <fun:Ident> "(" <args:InvokeArgs> ")" <e:@L> => Invoke1 { module: module.id, fun: fun.id, args, start: s, end: e },
    <s:@L> <module:Ident> "::" <fun:Ident> "(" ")" <e:@L> => Invoke1 { module: module.id, fun: fun.id, args: vec![], start: s, end: e },
}

InvokeArgs: Exprs1 = {
    <list:InvokeArgs> "," <item:Expr> => {
        let mut list = list;
        list.push(item);
        list
    },
    Expr => vec![<>],
}

#[inline]
Fields: Fields1 = {
    <fields:Sep<Fields, Field, ",">> => fields,
}

#[inline]
Ident: Ident = {
    "<ident>" => Ident { id: <>.0 },
}

#[inline]
StringLiteral: String = {
    "string" => <>.to_string(),
}

#[inline]
TestLiteral: String = {
    "test" => <>.to_string(),
}

Sep<L, T, D>: L = {
    <e:T> => vec![e],
    <v:L> D <e:T> => {
        let mut v = v;
        v.push(e);
        v
    },
    <v:L> D  => v,
}

#[inline]
BinOp<Op, Current, Next>: Expr1 = {
    <s:@L> <lhs:(<Current>)> <op:(<Op>)> @L <rhs:Next> <e:@L> => Expr1::Binary(BinExpr1 {
        kind: op,
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        start: s,
        end: e,
    }),
}

extern {
    type Location = Location;
    type Error = LexerError;
    enum Token<'input> {
        "<doc-comment>" => lexer::Token::DocComment(<&'input str>),
        "<single-line-comment>" => lexer::Token::SingleLineComment(<&'input str>),
        "let" => lexer::Token::Let,
        "match" => lexer::Token::Match,
        "of" => lexer::Token::Of,
        "end" => lexer::Token::End,
        "case" => lexer::Token::Case,
        "when" => lexer::Token::When,
        "for" => lexer::Token::For,
        "nil" => lexer::Token::Nil,
        "and" => lexer::Token::And,
        "or" => lexer::Token::Or,
        "not" => lexer::Token::Not,
        "drop" => lexer::Token::Drop,
        "emit" => lexer::Token::Emit,
        "default" => lexer::Token::Default,
        "patch" => lexer::Token::Patch,
        "insert" => lexer::Token::Insert,
        "upsert" => lexer::Token::Upsert,
        "update" => lexer::Token::Update,
        "erase" => lexer::Token::Erase,
        "merge" => lexer::Token::Merge,
        "event" => lexer::Token::Event,
        "present" => lexer::Token::Present,
        "absent" => lexer::Token::Absent,
        "." => lexer::Token::Dot,
        ";" => lexer::Token::Semi,
        "::" => lexer::Token::ColonColon,
        ":" => lexer::Token::Colon,
        "," => lexer::Token::Comma,
        "=" => lexer::Token::Eq,
//        "_" => lexer::Token::DontCare,
        "==" => lexer::Token::EqEq,
        "!=" => lexer::Token::NotEq,
        "{" => lexer::Token::LBrace,
        "}" => lexer::Token::RBrace,
        "(" => lexer::Token::LParen,
        ")" => lexer::Token::RParen,
        "." => lexer::Token::Dot,
        "$" => lexer::Token::Dollar,
        "=>" => lexer::Token::EqArrow,
        "[" => lexer::Token::LBracket,
        "]" => lexer::Token::RBracket,
        ">=" => lexer::Token::Gte,
        ">" => lexer::Token::Gt,
        "<=" => lexer::Token::Lte,
        "<" => lexer::Token::Lt,
        "+" => lexer::Token::Add,
        "-" => lexer::Token::Sub,
        "*" => lexer::Token::Mul,
        "/" => lexer::Token::Div,
        "%[" => lexer::Token::LPatBracket,
        "%{" => lexer::Token::LPatBrace,
        "%" => lexer::Token::Mod,
        "~=" => lexer::Token::TildeEq,
        "~" => lexer::Token::Tilde,
        "bool" => lexer::Token::BoolLiteral(<bool>),
        "int" => lexer::Token::IntLiteral(<i64>),
        "float" => lexer::Token::FloatLiteral(<f64>),
        "string" => lexer::Token::StringLiteral(<String>),
        "test" => lexer::Token::TestLiteral(<String>),
        "<ident>" => lexer::Token::Ident(<String>, <bool>),
        "<error>" => lexer::Token::BadToken(<String>),
        "<end-of-stream>" => lexer::Token::EndOfStream,
    }
}

